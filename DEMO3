**********************************************************************
*                                                                    *
*  THIS PROGRAM READS A SET OF INPUT LINES, EACH CONTAINING A THE    *
*  QUANTITY AND PRICE OF AN ITEM BEING ORDERED.  THE PROGRAM THEN:   *
*                                                                    *
*	1.  FINDS THE TOTAL COST OF THE ORDER                       *
*	2.  CUMPUTES THE AVERAGE NUMBER OF ITEMS PURCHASED          *
*	3.  FINDS THE AVERAGE COST PER ITEM FOR THE ORDER           *
*                                                                    *
*  INPUT IS TAKEN FROM AN IN-STREAM SET OF DATA.  IT IS ASSUMED THAT *
*  THE QUANTITY APPEARS BEFORE THE PRICE, BOTH NUMBERS APPEAR ON THE *
*  SAME LINE, AND THAT AT LEAST ONE BLANK SEPARATES THE TWO NUMBERS. *
*                                                                    *
**********************************************************************
	SPACE
GROCERY	CSECT
	USING GROCERY,15
	SPACE
R0	EQU	0
R1	EQU	1
R2	EQU	2
R3	EQU	3
R4	EQU	4
R5	EQU	5
R6	EQU	6
R7	EQU	7
R8	EQU	8
R9	EQU	9
R10	EQU	10
R11	EQU	11
R12	EQU	12
R13	EQU	13
R14	EQU	14
R15	EQU	15
	SPACE
	SR	R3,R3	CLEAR LINE COUNTER
	SR	R4,R4	CLEAR RUNNING TOTAL ITEMS
	SR	R5,R5	CLEAR RUNNING TOTAL COST
	SPACE
	XPRNT	EJECT,1	START AT TOP OF PAGE
	XPRNT HEADER,41	PRINT THE HEADER
	XPRNT SUBHEAD1,41	
	XPRNT	SUBHEAD2,42
	XPRNT SKIP,1	SPACE THE HEADER FROM DETAIL LINES
	SPACE
MORE	XREAD	CARD,80	READ A LINE
	BC	B'0100',FIGURE	GO DO CALCS ON EOF
	SPACE
	XDECI	R9,CARD	GET QUANTITY
	XDECI	R8,0(R1)	GET THE PRICE
	LR	R7,R8
	MR	R6,R9	CALCULATE COST FOR THE ITEM
	SPACE
	XDECO	R9,PLINE+1	PUT QUANTITY INTO PRINT BUFFER
	XDECO	R8,PLINE+15	PUT PRICE TO PLINE
	XDECO	R7,PLINE+29	PUT COST INTO PLINE
	XPRNT PLINE,41	PRINT QTY, PRICE AND COST
	SPACE
	LA	R3,1(R3)	INCREMENT ITEM TOTAL
	AR	R4,R9	ADD QUANTITY TO TOTAL OF ITEMS
	AR	R5,R7	ADD COST TO RUNNING TOTAL
	BC	B'1111',MORE	LOOP FOR MORE
	SPACE
FIGURE	SR	R10,R10	CALCULATE THE AVERAGE NUMBER ITEMS
	LR	R11,R4
	DR	R10,R3
	SPACE
	SR	R12,R12	CALCULATE THE AVERAGE COST PER ITEM
	LR	R13,R5
	DR	R12,R4
	SPACE
	XDECO	R11,NUM$LABL+41	PLACE AVERAGE NUMBER IN PRINT LINE
	XPRNT NUM$LABL,53		AND PRINT IT
	XDECO	R13,COST$LAB+29	PLACE AVERAGE COST IN PRINT LINE 
	XPRNT COST$LAB,48		AND PRINT IT
	XPRNT EJECT,1	CLEAR THE LISTING
	SPACE
	BCR	B'1111',R14	EXIT PROGRAM
	SPACE
CARD	DS	CL80	INPUT LINE BUFFER
PLINE	DC	CL80' '	PRINT LINE BUFFER
EJECT	DC	C'1'	CARRIAGE CTRL TO GO TO TOP OF PAGE
SKIP	DC	C' '	CARRIAGE CTRL TO PRINT BLANK LINE
HEADER	DC	CL41'0                PRICE EACH    TOTAL COST'
SUBHEAD1	DC	CL41'     QUANTITY    (IN CENTS)    (IN CENTS)'
SUBHEAD2 DC	CL42'    ==========  ============  ============'
NUM$LABL	DC	CL53'- THE AVERAGE NUMBER OF ITEMS PURCHASED ='
COST$LAB	DC	CL48'0 THE AVERAGE COST PER ITEM =             CENTS.'
	END GROCERY
$ENTRY
1       55
3      121
1      247
6       15
3       75
